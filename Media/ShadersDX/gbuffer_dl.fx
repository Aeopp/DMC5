
uniform sampler2D specularMap	: register(s0);
uniform sampler2D normalMap		: register(s1);

uniform matrix matWorld;
uniform matrix matWorldInv;
uniform matrix matViewProj;

uniform float2 uv				= { 1, 1 };

uniform float specularPower		= 80.0f;
uniform float hasTexture		= 1.0f;
uniform float handedness		= -1.0f;

void vs_gbuffer(
	in out	float4 pos		: POSITION,
	in		float3 norm		: NORMAL,
	in out	float2 tex		: TEXCOORD0,
	out		float2 zw		: TEXCOORD1,
	out		float3 wnorm	: TEXCOORD2)
{
	pos = mul(float4(pos.xyz, 1), matWorld);
	wnorm = mul(matWorldInv, float4(norm, 0)).xyz;

	pos = mul(pos, matViewProj);
	zw = pos.zw;

	tex *= uv;
}

void ps_gbuffer(
	in	float2 tex		: TEXCOORD0,
	in	float2 zw		: TEXCOORD1,
	in	float3 wnorm	: TEXCOORD2,
	out float4 color0	: COLOR0,	// normals + shininess
	out float4 color1	: COLOR1)	// depth
{
	float3 n = normalize(wnorm);
	float4 spec = tex2D(specularMap, tex);

	spec.r = lerp(specularPower, spec.r, hasTexture);

	color0 = float4(n * 0.5f + 0.5f, spec.r * 1e-3f);
	color1 = float4(zw.x / zw.y, 0, 0, 0);
}

void vs_gbuffer_tbn(
	in out	float4 pos		: POSITION,
	in		float3 norm		: NORMAL,
	in		float3 tang		: TANGENT,
	in		float3 bin		: BINORMAL,
	in out	float2 tex		: TEXCOORD0,
	out		float2 zw		: TEXCOORD1,
	out		float3 wnorm	: TEXCOORD2,
	out		float3 wtan		: TEXCOORD3,
	out		float3 wbin		: TEXCOORD4)
{
	pos = mul(float4(pos.xyz, 1), matWorld);

	wnorm = mul(matWorldInv, float4(norm, 0)).xyz;
	wtan = mul(float4(tang, 0), matWorld).xyz;
	wbin = mul(float4(bin, 0), matWorld).xyz;

	pos = mul(pos, matViewProj);
	zw = pos.zw;

	tex *= uv;
}

void ps_gbuffer_tbn(
	in	float2 tex		: TEXCOORD0,
	in	float2 zw		: TEXCOORD1,
	in	float3 wnorm	: TEXCOORD2,
	in	float3 wtan		: TEXCOORD3,
	in	float3 wbin		: TEXCOORD4,
	out float4 color0	: COLOR0,	// normals + shininess
	out float4 color1	: COLOR1)	// depth
{
	float3x3 tbn = { wtan, handedness * wbin, wnorm };

	float4 spec = tex2D(specularMap, tex);
	float3 tnorm = tex2D(normalMap, tex).rgb * 2.0f - 1.0f;
	float3 n = normalize(mul(tbn, tnorm));

	spec.r = lerp(specularPower, spec.r, hasTexture);

	color0 = float4(n * 0.5f + 0.5f, spec.r * 1e-3f);
	color1 = float4(zw.x / zw.y, 0, 0, 0);
}

technique gbuffer
{
	pass p0
	{
		vertexshader = compile vs_3_0 vs_gbuffer();
		pixelshader = compile ps_3_0 ps_gbuffer();
	}
}

technique gbuffer_tbn
{
	pass p0
	{
		vertexshader = compile vs_3_0 vs_gbuffer_tbn();
		pixelshader = compile ps_3_0 ps_gbuffer_tbn();
	}
}
